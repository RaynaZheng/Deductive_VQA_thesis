formulas(assumptions).

% mereology --- part()
(all x
	(part(x,x))).

(all x all y
	((part(x,y)
	& part(y,x))
	->
	(x=y))).

(all x all y all z
	((part(x,y)
	& part(y,z))
	->
	part(x,z))).

% def: overlaps()
(all x all y
	(overlaps(x,y)
	<->
	(exists z
		(part(z,x)
		& part(z,y))))).

(all x all y
	(-part(x,y)
	->
	(exists z
		(part(z,x)
		& -overlaps(z,y))))).

% def: proper part --- ppart()
(all x all y
	(ppart(x,y)
	<->
	(part(x,y) & (x != y)))).

% def: sum()
(all x all y all z
	(sum(x,y,z)
	<->
	(all v
		(overlaps(v,z)
		<->
		(overlaps(v,x)
		| overlaps(v,y)))))).

% def: atom()
(all x 
	(atom(x) 
	<->  
	(all y (part(y, x) -> (y = x))))).

(all x all y all z
	(sum(x,y,z)
	-> 
	(part(x,z)))).

% def: minimal upper bound --- mub()
(all x all y all z
	(mub(x,y,z)
	<->
	(part(x,z)
	& part(y,z)
	& -(exists u
		(part(x,u)
		& part(y,u)
		& ppart(u,z)))))).

% mub properties

(all x all y
	(exists z
		(mub(x,y,z)))).

(all x 
	(mub(x,x,x))).

(all x all y all z
	(mub(x,y,z) -> mub(y,x,z))).

(all x all y 
	(overlaps(x,y)
	-> 
	(exists z 
		(sum(x,y,z))))).

% covers def
(all x all y
        (covers(x,y)
        <->
        (ppart(y,x) & -(exists z
                (ppart(y,z) & ppart(z,x)))))).

(all x exists y 
	(atom(y) & part(y,x))).

(all x all y
	(ppart(x,y)    
	->
	(exists z
		(covers(z,x) & part(z,y))))).

(all x all y
	(ppart(x,y)    
	->
	(exists z
		(covers(y,z) & part(x,z))))).

end_of_list.

formulas(goals).

(all x all y all z
	((atom(z) & sum(z,y,x) & -part(z,y))
	->
	covers(x,y))).

end_of_list.